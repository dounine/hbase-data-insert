group 'com.dounine'
version '1.0.0-SNAPSHOT'

buildscript {
    repositories {
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }
    dependencies {
        classpath "com.github.jengelman.gradle.plugins:shadow:4.0.2"
    }
}

apply plugin: "com.github.johnrengelman.shadow"
apply plugin: 'java'
apply plugin: 'scala'

sourceCompatibility = 1.8

repositories {
    mavenLocal()
    mavenCentral()
    maven { url "http://repo.hortonworks.com/content/repositories/releases/" }
}

configurations {
    provided
}

task copyJars(type: Copy) {
    from configurations.runtime
    into new File('build/libs/lib')
}
compileJava.dependsOn copyJars

shadowJar {
    zip64 true
}

def env = hasProperty("pro") ? "RELEASE" : "SNAPSHOT"

sourceSets {
    main {
        compileClasspath += configurations.provided
        resources {
            srcDirs = ["src/main/resources", "src/main/profile/$env"]
        }
    }
}

jar {
    manifest {
        attributes 'Implementation-Title': 'HbaseDataInsert',
                'Implementation-Version': version,
                'Main-Class': 'com.dounine.hbase.BulkLoad'
    }
}

dependencies {
    compile 'org.scala-lang:scala-library:2.11.12'

    compile group: 'org.apache.spark', name: 'spark-sql_2.11', version: '2.3.1.3.0.0.0-1634'
    compile group: 'org.apache.hbase', name: 'hbase-it', version: '2.0.0.3.0.0.0-1634'

}
